@echo off
setlocal enabledelayedexpansion
:: ============================================================================
::  SYNOPSIS
::	set _SPRTOP_=<Springheadのトップディレクトリ>
::	do_python.bat script.py [args..]	（Windows 専用）
::
::  重大な変更
::	この do_python.bat は廃止し、setup 機構で必要な設定 (swig の生成、
::	パスの設定) を行なうように変更する。
::	ただし移行期間を設け、その間は setup file が存在しない場合に限り
::	従来の処理を実行する (存在するときは何もしない)。
::
::  DESCRIPTION
::	Python script を実行するための Windows 用アダプタ。
::	"Springhead/buildtool" が利用できるときは、それを使って実行する。
::	さもなければ、デフォルトで利用できる python を使って実行する。
::	デフォルトで python が利用できない場合、または python のバージョンが
::	3 より古い場合は、メッセージを表示して処理を中止する。
::	
::	このファイルは Windows 専用である。またSpringehadのトップディレクトリ
::	を取得するために環境変数 "_SPRTOP_" を使用する。本スクリプトを呼ぶ前に
::	この環境変数を設定しておくこと。
::	unix の場合はデフォルトで Python が使用できるようにしておかなければ
::	ならない。
::
::  VERSION
::	Ver 1.0  2019/10/10 F.Kanehori	RunSwig から移動. -SprTop は廃止.
::	Ver 1.1  2020/04/16 F.Kanehori	_SPRTOP_ のデフォルトを変更.
::	Ver 2.0  2020/05/09 F.Kanehori	_SPRTOP_ は廃止.
::	Ver 3.0  2020/12/07 F.Kanehori	Setup 導入移行期間開始.
:: ============================================================================
set CWD=%CD%
set verbose=0

::-----------------------------------------------------------------------------
:: Springhead tree のトップへ移動する
::	このスクリプトは "<top>/core/src" に置く。
::
cd /d %~dp0
cd ..\..
set SprTop=%CD%
cd %CWD%

::+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
:: 移行期間の処理
::	移行期間終了後は _wrapper python args.. を呼び出す
::	だけにする
::
if exist %SprTop%\core\src\setup.conf (
	echo 移行処理 -^> _wrapper python %*
	cmd /c ..\_wrapper.bat python %*
	exit /b 0
)
echo do_python: invoke DOS batch
::+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

if exist buildtool\ (
	set TOOLPATH=%SprTop%\buildtool\win32
	if %verbose% geq 1 (
		echo buildtool found at "%SprTop%\buildtool"
	)
)

:: 引数はそのまま渡す
set ARGS=%*

::----------------------------------------------
::  Python を実行できるようにする
::
if exist "%TOOLPATH%\python.exe" (
	PATH=!TOOLPATH!;!PATH!
) else (
	where python >NUL 2>& 1
	if !ERRORLEVEL! neq 0 (
		echo Python not found.
		endlocal
		exit /b
	)
	for /f "tokens=*" %%a in ('python -V') do set OUT=%%a
	set VER=!OUT:Python =!
	set MAJOR=!VER:~0,1!
	set MINOR=!VER:~2,1!
	if !MAJOR! neq 3 (
		echo !OUT! found.
		echo Use Python 3 or later version.
		endlocal
		exit /b
	)
)
if %verbose% geq 1 (
	rem where python
	python -V
)

::----------------------------------------------
::  Python を実行する
::
if %verbose% geq 2 (
	echo cwd: %CD%
	echo python %ARGS%
)
rem if %verbose% geq 1 (
rem 	echo.
rem )
rem echo python %ARGS%
python %ARGS%

endlocal
exit /b

::----------------------------------------------
:leaf
	set _ret_=%~nx1
	exit /b

